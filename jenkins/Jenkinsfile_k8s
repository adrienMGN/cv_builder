pipeline {
    agent any

    environment {
        VENV_PATH = "${WORKSPACE}/venv_ansible"
        MASTER_IP = "100.120.92.90" 
    }

    stages {
        stage('Checkout Code') {
            steps {
                checkout scm
            }
        }

        stage('Setup Ansible') {
            steps {
                sh """
                    python3 -m venv ${VENV_PATH}
                    ${VENV_PATH}/bin/pip install --upgrade pip
                    ${VENV_PATH}/bin/pip install ansible jmespath
                """
            }
        }

        stage('Clean Nodes') {
            steps {
                sshagent(['k8s-nodes-ssh-key', 'master-specific-key']) {
                    sh """
                        echo "Nettoyage des résidus..."
                        ${VENV_PATH}/bin/ansible all -i jenkins/hosts.ini -b -m shell -a "
                            if [ -f /usr/local/bin/k3s-uninstall.sh ]; then /usr/local/bin/k3s-uninstall.sh; fi;
                            if [ -f /usr/local/bin/k3s-agent-uninstall.sh ]; then /usr/local/bin/k3s-agent-uninstall.sh; fi;
                            rm -rf /etc/rancher /var/lib/rancher /var/lib/kubelet /etc/kubernetes /usr/local/bin/kubectl
                        "
                    """
                }
            }
        }

        stage('Install K3s Master') {
            steps {
                sshagent(['k8s-nodes-ssh-key', 'master-specific-key']) {
                    sh """
                        echo "Installation du Master K3s..."
                        ${VENV_PATH}/bin/ansible master-node-01 -i jenkins/hosts.ini -b -m shell -a "
                            curl -sfL https://get.k3s.io | INSTALL_K3S_EXEC='--disable traefik --node-name master-node-01' sh -
                        "
                    """
                }
            }
        }

        stage('Install K3s Workers') {
            steps {
                sshagent(['k8s-nodes-ssh-key', 'master-specific-key']) {
                    script {
                        // On récupère le token brute
                        String rawOutput = sh(
                            script: "${VENV_PATH}/bin/ansible master-node-01 -i jenkins/hosts.ini -b -a 'cat /var/lib/rancher/k3s/server/node-token'",
                            returnStdout: true
                        ).trim()
                        
                        // Extraction sécurisée sans utiliser d'objets non-sérialisables (Matcher) dans le scope principal
                        String k3sToken = extractToken(rawOutput)

                        echo "Token extrait : ${k3sToken.substring(0, 10)}..."

                        sh """
                            ${VENV_PATH}/bin/ansible kube_node -i jenkins/hosts.ini -b -m shell -a "
                                curl -sfL https://get.k3s.io | K3S_URL=https://${MASTER_IP}:6443 K3S_TOKEN=${k3sToken} sh -s - agent --node-name {{ inventory_hostname }}
                            "
                        """
                    }
                }
            }
        }

        stage('Verify Cluster') {
            steps {
                sshagent(['k8s-nodes-ssh-key', 'master-specific-key']) {
                    sh """
                        ${VENV_PATH}/bin/ansible master-node-01 -i jenkins/hosts.ini -b -m shell -a "kubectl get nodes"
                    """
                }
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}

// Fonction décorée pour éviter le bug de sérialisation
@NonCPS
def extractToken(String text) {
    def matcher = (text =~ /K10[a-f0-9:]+::server:[a-f0-9]+/)
    if (matcher) {
        return matcher[0]
    }
    return null
}
